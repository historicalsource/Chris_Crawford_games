;
;

	ORG	$FA
SCNPTR	ORG	*+2
CHRPTR	ORG	*+2
;
	ORG	CALIG
	JMP	CALIGS
;
;
OFFSET	ORG	*+1
CALCHR	ORG	*+1
WIDFLG	ORG	*+1
LINCNT	ORG	*+1
BUFF1	ORG	*+1
BUFF2	ORG	*+1
BUFF3	ORG	*+1
BUFF4	ORG	*+1
MASK	ORG	*+1
CHRBYT	ORG	*+1
CHARX	ORG	*+1
WIDTH	ORG	*+1
HEIGHT	ORG	*+1
LENGTH	ORG	*+1
TAIL	ORG	*+1
;
;
CALIGS:	; START CALIG
;
;
;SUBR CALIG PUTS THE GOTHIC LETTERS
;ON THE SCREEN. ON ENTRY: A=LETTER
;   TO PRINT IN ASCII (HEX)
;X=OFFSET FROM BEG OF ROW IN PIXELS
;PADPTR POINTS TO THE PLACE IN
;SCREEN RAM WHERE THE CHAR WILL GO
	SEC
	SBC	#$41	;GET INDEX OF CHAR IN TABLE
	STA	CALCHR
	TXA
	STX	CHARX	;STORE UNTIL END OF PROC.
	LSR	A	;GET BYTE COUNT FROM X
	LSR	A
	LSR	A
	CLC
	ADC	PADPTR
	STA	SCNPTR     	;SCNPTR POINTS TO 1ST
	LDA	PADPTR+1	;BYTE WHICH WILL BE
	ADC	#0		;PRINTED TO
	STA	SCNPTR+1
	TXA
	AND	#7		;GET OFFSET WITHIN BYTE
	STA	OFFSET           
	LDA	CALCHR	;POINT TO WHERE CHAR DATA IS
	CLC
	ASL	A
	TAX
	LDA	TABLE,X
	STA	CHRPTR
	LDA	TABLE+1,X
	STA	CHRPTR+1
;
	LDY	#0		;GET CHAR WIDTH AND HEIGHT
	LDA	(CHRPTR),Y	;AND TAIL HEIGHT
	CMP	#25
	BCC	LAB01
	LDA	#24
LAB01:
	STA	WIDTH
	INY
	LDA	(CHRPTR),Y
	CMP	#26
	BCC	LAB02
	LDA	#25
LAB02:
	STA	HEIGHT
	INY
	LDA	(CHRPTR),Y
	CMP	#6
	BCC	LAB03
	LDA	#5
LAB03:
	STA	TAIL
;
	LDA	CHRPTR	    ;INCREMENT POINTER BY 3 TO
	CLC				;POINT TO DATA ONLY
	ADC	#3
	STA	CHRPTR
	LDA	CHRPTR+1
	ADC	#0
	STA	CHRPTR+1
	LDA	#26	;GET HOW MANY LINES TO
	SEC		;SKIP BEFORE PRINTING CHAR
	SBC	HEIGHT
	TAX
LOOP30	LDA	SCNPTR	;SKIPS 27-HEIGHT LINES
	CLC		;SKIP IS DONE BY ADDING
	ADC	LINLEN	;LINE LENGTH TO SCNPTR
	STA	SCNPTR
	BCC	Y01
	INC	SCNPTR+1	;IF HIGH BYTE INCREMENTS
Y01	DEX
	BPL	LOOP30
;
	LDX	#2	;CHAR EITHER 2 OR 3 BYTES WIDE
	LDA	WIDTH
	CMP	#17
	BMI	Y04
	INX
Y04	STX	WIDFLG
;
	LDA	#0	;INDEX INTO DATA DESC
	STA	CHRBYT
	LDA	HEIGHT
	CLC
	ADC	TAIL		;TOTAL HEIGHT
	STA	LENGTH
	STA	LINCNT	;HOW MANY LINES TO PRINT
	ASL	LENGTH	;*2 FOR 2 BYTES WIDE
	LDA	WIDFLG
	CMP	#2
	BEQ	TWOWD
	LDA	LENGTH	;IF 3 BYTES WIDE THEN
	CLC		;ADD ANOTHER HEIGHT
	ADC	HEIGHT	;AND TAIL
	ADC	TAIL
	STA	LENGTH
	JMP	THREWD
;
;
TWOWD	LDY	CHRBYT	;THE CHAR IS 2 BYTES WIDE
	JSR	FILBUF
	LDA	#0
	STA	BUFF3
	LDA	#0
	STA	BUFF4
	JSR	SHIFT
	DEC	LINCNT	;HOW MANY MORE LINES
	LDA	LINCNT	;TO PRINT OUT
	BEQ	DONE
	LDA	CHRBYT	;POINT TO NEXT ROW OF DATA
	CLC
	ADC	#2
	STA	CHRBYT
	JSR	NXTROW
	JMP	TWOWD
DONE	JMP	Y06
;
;
THREWD	LDY	CHRBYT	;CHAR IS 3 BYTES WIDE
	JSR	FILBUF
	LDA	(CHRPTR),Y
	STA	BUFF3
	INY
	LDA	#0
	STA	BUFF4
	JSR	SHIFT
	INY
	LDA	BUFF4
	STA	(SCNPTR),Y
	DEC	LINCNT	;HOW MANY LINES LEFT
	LDA	LINCNT	;TO PRINT
	BEQ	Y06
	LDA	CHRBYT
	CLC
	ADC	#3
	STA	CHRBYT
	JSR	NXTROW
	JMP	THREWD
;
;
Y06	LDA	WIDTH;CHAR IS FINISHED
	CLC		;POINT X OFFSET TO
	ADC	CHARX	;BEYOND THE CHAR IF NEXT CALL
	ADC	#2	;TO "CALIG" WILL BE IMMEDIATELY
	TAX
	RTS
;
;
	;SUBROUTINE FILBUF FILLS IN BUFFERS 1 AND 2
FILBUF	LDA	(CHRPTR),Y
	STA	BUFF1
	INY
	LDA	(CHRPTR),Y
	STA	BUFF2
	INY
	RTS
;
;
	;SUBROUTINE SHIFT
SHIFT	LDA	#0
	STA	MASK
	LDX	OFFSET
	BEQ	Y05	;DON'T SHIFT IF ON BYTE BOUNDARY

LOOP31	;SHIFT PROPER AMOUNT TO PULL
	;CHAR DATA BYTE BOUNDARY TO
	;PRINT BOUNDARY.   FOLD IN
	;MASK FOR 1ST BYTE AUTO GENERATED
	SEC
	ROR	MASK
	LSR	BUFF1
	ROR	BUFF2
	ROR	BUFF3
	ROR	BUFF4
	DEX
	BNE	LOOP31
;
Y05	LDY	#0
	LDA	(SCNPTR),Y	;FOLD IN 1ST SHIFTED BYTE
	EOR	BUFF1	;SO DOES NO ERASE LATTER PART
	AND	MASK	;OF LAST CHAR
	EOR	BUFF1
	STA	(SCNPTR),Y
	INY
	LDA	BUFF2	;STORE REST OF SHIFTED PATTERN
	STA	(SCNPTR),Y
	INY
	LDA	BUFF3
	STA	(SCNPTR),Y
	RTS
;
;
	;SUBROUTINE NXTROW MAKES  SCNPTR POINT TO
	;THE NEXT ROW DOWN (THE NEXT SCAN LINE)
NXTROW	LDA	SCNPTR
	CLC
	ADC	LINLEN
	STA	SCNPTR
	LDA	SCNPTR+1
	ADC	#0
	STA	SCNPTR+1
	RTS
;
;
TABLE	DW	UA
	DW	UB
	DW	UC
	DW	UD
	DW	UE
	DW	UF
	DW	UG
	DW	UH
	DW	UI
	DW	UJ
	DW	UK
	DW	UL
	DW	UM
	DW	UN
	DW	UO
	DW	UP
	DW	UQ
	DW	UR
	DW	US
	DW	UT
	DW	UU
	DW	UV
	DW	UW
	DW	UX
	DW	UY
	DW	UZ
	DW	0,0,0
	DW	0,0,0
	DW	LA
	DW	LB
	DW	LC
	DW	LD
	DW	LOWE
	DW	LF
	DW	LG
	DW	LH
	DW	LI
	DW	LJ
	DW	LK
	DW	LL
	DW	LM
	DW	LN
	DW	LO
	DW	LP
	DW	LQ
	DW	LR
	DW	LS
	DW	LOWT
	DW	LU
	DW	LV
	DW	LW
	DW	LX
	DW	LY
	DW	LZ
	DW	ZERO
	DW	ONE
	DW	TWO
	DW	THREE
	DW	FOUR
	DW	FIVE
	DW	SIX
	DW	SEVEN
	DW	EIGHT
	DW	NINE

LA	DB	13,16,0
	DB	3,0,7,$90,$1F,$E0
	DB	$67,$C0,$C3,$C0,$C3,$C0
	DB	$C3,$C0,$C3,$C0,$C3,$C0
	DB	$C3,$C0,$C3,$C0,$C3,$C8
	DB	$C3,$F0,$C7,$E0,$79,$E0
	DB	$30,$C0

LM	DB	24,16,0
	DB	$18,$30,$60
	DB	$3C,$78,$F2
	DB	$7E,$FD,$FC
	DB	$9F,$3E,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$79
	DB	$1E,$3C,$7E
	DB	$1E,$3C,$3C
	DB	$C,$18,$18

LOWE	DB	12,16,0
	DB	$1,$C0,$F,$C0
	DB	$3D,$E0,$3D,$E0
	DB	$3C,$F0,$3C,$E0
	DB	$3C,$80,$3D,$00
	DB	$3E,$0,$3C,$0
	DB	$3E,$0,$7F,$20
	DB	$9F,$C0,$F,$80
	DB	$7,$0,$3,$0

LO	DB	14,16,0
	DB	$1,$80,$3,$C0
	DB	7,$E4,$19,$F8
	DB	$38,$F0,$38,$70
	DB	$38,$70,$38,$70
	DB	$38,$70,$38,$70
	DB	$38,$70,$3C,$70
	DB	$7E,$60,$9F,$80
	DB	$F,$0,$6,$0

UC	DB	20,23,0
	DB	$0,$C1,$80
	DB	$3,$7,$90
	DB	$E,$1F,$E0
	DB	$1C,$37,$E0
	DB	$3C,$F3,$C0
	DB	$39,$F1,$80
	DB	$7A,$F2,$0
	DB	$7C,$F2,$0
	DB	$78,$F2,$0
	DB	$F8,$F2,$0
	DB	$F0,$F2,$0
	DB	$F0,$F2,$0
	DB	$F0,$F2,$0
	DB	$F0,$F2,$0
	DB	$F8,$F2,$0
	DB	$F8,$F2,$0
	DB	$78,$F2,$0
	DB	$78,$C2,$0
	DB	$3F,$2,$20
	DB	$3C,$2,$40
	DB	$1C,$1,$80
	DB	$E,$6,$0
	DB	$3,$F8,$0

LL	DB	10,23,0
	DB	$0,$0,$C,$0
	DB	$3C,$0,$3C,$0
	DB	$3C,$0,$3C,$0
	DB	$3C,$0,$3C,$0
	DB	$3C,$0,$3C,$0
	DB	$3C,$0,$3C,$0
	DB	$3C,$0,$3C,$0
	DB	$3C,$0,$3C,$0
	DB	$3C,$0,$3C,$0
	DB	$3C,$0,$3E,$40
	DB	$7F,$80,$9E,$0
	DB	$C,$0

LOWT	DB	10,23,0
	DB	$0,$0,$0,$0
	DB	$6,$0,$1E,$0
	DB	$1E,$0,$1E,$0
	DB	$1E,$0,$1E,$0
	DB	$FF,$C0,$FF,$C0
	DB	$FF,$C0,$1E,$0
	DB	$1E,$0,$1E,$0
	DB	$1E,$0,$1E,$0
	DB	$1E,$0,$1E,$0
	DB	$1E,$0,$1E,$40
	DB	$1F,$80,$F,$0
	DB	$6,$0
LB	DB	14,22,0
	DB	$0C,$00,$3C,$00
	DB	$3C,$00,$3C,$00
	DB	$3C,$00,$3C,$00
	DB	$3C,$C0,$3D,$E4
	DB	$3E,$F8,$3C,$70
	DB	$3C,$70,$3C,$70
	DB	$3C,$70,$3C,$70
	DB	$3C,$70,$3C,$70
	DB	$3C,$70,$3E,$70
	DB	$7F,$70,$9F,$80
	DB	$0F,$00,$06,$00
LC	DB	11,16,0
	DB	$01,$80,$07,$C0
	DB	$1D,$E0,$3C,$C0
	DB	$3C,$00,$3C,$00
	DB	$3C,$00,$3C,$00
	DB	$3C,$00,$3C,$00
	DB	$3C,$00,$3C,$00
	DB	$7E,$40,$9F,$80
	DB	$0F,$00,$06,$00
LD	DB	15,22,0
	DB	$30,$00,$38,$00
	DB	$3C,$00,$3E,$00
	DB	$1F,$00,$0F,$80
	DB	$07,$C0,$03,$E0
	DB	$07,$F2,$3C,$FC
	DB	$3C,$78,$3C,$78
	DB	$3C,$78,$3C,$78
	DB	$3C,$78,$3C,$78
	DB	$3C,$78,$3E,$78
	DB	$7F,$78,$9F,$C0
	DB	$0F,$00,$06,$00
LF	DB	11,22,3
	DB	$00,$C0,$01,$E0
	DB	$07,$E0,$3D,$E0
	DB	$3C,$C0,$3C,$00
	DB	$3C,$00,$FF,$00
	DB	$FF,$00,$FF,$00
	DB	$3C,$00,$3C,$00
	DB	$3C,$00,$3C,$00
	DB	$3C,$00,$3C,$00
	DB	$3C,$00,$3C,$00
	DB	$3C,$00,$3C,$00
	DB	$3C,$00,$3C,$00
	DB	$20,$00,$20,$00
	DB	$20,$00
LG	DB	13,16,5
	DB	$03,$00,$07,$80
	DB	$3F,$C8,$F7,$F0
	DB	$F1,$E0,$F1,$E0
	DB	$F1,$E0,$F1,$E0
	DB	$F1,$E0,$F1,$E0
	DB	$F1,$E0,$F1,$E0
	DB	$F9,$E0,$FD,$E0
	DB	$F9,$F0,$70,$F8
	DB	$00,$78,$70,$30
	DB	$FC,$60,$3C,$C0
	DB	$1F,$00
LH	DB	14,22,5
	DB	$18,$00,$78,$00
	DB	$78,$00,$78,$00
	DB	$78,$00,$78,$64
	DB	$78,$F8,$79,$F0
	DB	$7B,$F0,$7C,$F0
	DB	$78,$F0,$78,$F0
	DB	$78,$F0,$78,$F0
	DB	$78,$F0,$78,$F0
	DB	$78,$F0,$78,$F0
	DB	$78,$F0,$FC,$F0
	DB	$78,$F0,$30,$F0
	DB	$00,$F0,$00,$E0
	DB	$00,$C0,$01,$80
	DB	$02,$00
LI	DB	10,21,0
	DB	$0C,$00,$1E,$00
	DB	$3F,$00,$1E,$00
	DB	$0C,$00,$00,$00
	DB	$18,$00,$3C,$80
	DB	$7F,$00,$9E,$00
	DB	$1E,$00,$1E,$00
	DB	$1E,$00,$1E,$00
	DB	$1E,$00,$1E,$00
	DB	$1E,$00,$1E,$40
	DB	$1F,$80,$0F,$00
	DB	$06,$00
LJ	DB	9,22,2
	DB	$0C,$00,$1E,$00
	DB	$3F,$00,$1E,$00
	DB	$0C,$00,$00,$00
	DB	$18,$00,$3C,$80
	DB	$7F,$00,$9E,$00
	DB	$1E,$00,$1E,$00
	DB	$1E,$00,$1E,$00
	DB	$1E,$00,$1E,$00
	DB	$1E,$00,$1E,$00
	DB	$1E,$00,$1E,$00
	DB	$1E,$00,$1E,$00
	DB	$1E,$00,$18,$00
LK	DB	15,22,0
	DB	$18,$00,$78,$00
	DB	$78,$00,$78,$00
	DB	$78,$00,$78,$00
	DB	$78,$C0,$79,$E4
	DB	$7B,$F8,$7D,$F0
	DB	$78,$F0,$78,$F0
	DB	$78,$E0,$7B,$00
	DB	$7C,$C0,$78,$F0
	DB	$78,$F0,$78,$F2
	DB	$78,$FC,$FC,$F8
	DB	$78,$F0,$30,$60
LN	DB	18,15,0
	DB	$18,$30,$00
	DB	$3C,$79,$00
	DB	$7E,$FE,$00
	DB	$9F,$3C,$00
	DB	$1E,$3C,$00
	DB	$1E,$3C,$00
	DB	$1E,$3C,$00
	DB	$1E,$3C,$00
	DB	$1E,$3C,$00
	DB	$1E,$3C,$00
	DB	$1E,$3E,$40
	DB	$1E,$3F,$80
	DB	$3F,$3F,$00
	DB	$1E,$1E,$00
	DB	$0C,$0C,$00
LP	DB	15,16,5
	DB	$18,$30,$3C,$78
	DB	$7E,$FE,$9F,$3C
	DB	$1E,$3C,$1E,$3C
	DB	$1E,$3C,$1E,$3C
	DB	$1E,$3C,$7E,$3C
	DB	$7E,$3C,$FE,$3C
	DB	$9F,$30,$1F,$C0
	DB	$1F,$80,$1F,$00
	DB	$1E,$00,$1E,$00
	DB	$1E,$00,$1E,$00
	DB	$18,$00
LQ	DB	13,16,5
	DB	$03,$00,$07,$80
	DB	$1F,$C8,$F3,$F0
	DB	$F1,$E0,$F1,$E0
	DB	$F1,$E0,$F1,$E0
	DB	$F1,$E0,$F1,$E0
	DB	$F1,$E0,$F1,$E0
	DB	$FB,$E0,$FD,$E0
	DB	$79,$E0,$31,$E0
	DB	$01,$E0,$01,$E0
	DB	$01,$E0,$01,$E0
	DB	$01,$80
LR	DB	16,16,0
	DB	$0C,$30,$1E,$79
	DB	$3F,$FE,$7E,$7C
	DB	$9E,$38,$1E,$00
	DB	$1E,$00,$1E,$00
	DB	$1E,$00,$1E,$00
	DB	$1E,$00,$1E,$00
	DB	$1F,$20,$0F,$C0
	DB	$07,$80,$03,$00
LS	DB	16,16,0
	DB	$00,$C0,$08,$E2
	DB	$07,$FC,$3C,$F8
	DB	$3C,$10,$3C,$39
	DB	$3C,$7E,$3C,$BC
	DB	$3D,$3C,$7E,$3C
	DB	$9C,$3C,$08,$3C
	DB	$1F,$3C,$3F,$E0
	DB	$47,$80,$03,$00
LU	DB	18,16,0
	DB	$0C,$0C,$80
	DB	$1E,$1F,$00
	DB	$3F,$3E,$00
	DB	$7F,$3E,$00
	DB	$9F,$1E,$00
	DB	$0F,$1E,$00
	DB	$0F,$1E,$00
	DB	$0F,$1E,$00
	DB	$0F,$1E,$00
	DB	$0F,$1E,$00
	DB	$0F,$1E,$00
	DB	$0F,$1E,$00
	DB	$0F,$BE,$40
	DB	$0F,$DF,$80
	DB	$07,$8F,$00
	DB	$03,$06,$00
LV	DB	16,16,0
	DB	$0C,$30,$1E,$79
	DB	$3E,$FE,$7F,$3C
	DB	$9E,$3C,$1E,$3C
	DB	$1E,$3C,$1E,$3C
	DB	$1E,$3C,$1E,$3C
	DB	$1E,$3C,$1F,$3C
	DB	$3F,$BC,$4F,$E0
	DB	$07,$80,$03,$00
LW	DB	23,16,0
	DB	$0C,$30,$60
	DB	$1E,$78,$F2
	DB	$3E,$FD,$FC
	DB	$7F,$3E,$78
	DB	$9E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1E,$3C,$78
	DB	$1F,$7E,$E0
	DB	$0F,$9F,$00
	DB	$06,$0C,$00
LX	DB	18,16,0
	DB	$0E,$07,$00
	DB	$3F,$CF,$C0,$67,$EF,$C0
	DB	$03,$E9,$80,$01,$F0,$00
	DB	$01,$E0,$00,$01,$E0,$00
	DB	$07,$F8,$00,$07,$F8,$00
	DB	$01,$E0,$00,$01,$E0,$00
	DB	$03,$E0,$00,$85,$F0,$00
	DB	$CD,$F9,$80,$FC,$FF,$00
	DB	$70,$1C,$00
LY	DB	15,17,4
	DB	$0C,$18,$1E,$3D
	DB	$3E,$7E,$7E,$3C
	DB	$9E,$3C,$1E,$3C
	DB	$1E,$3C,$1E,$3C
	DB	$1E,$3C,$1E,$3C
	DB	$1E,$3C,$1E,$3C
	DB	$1E,$3C,$1F,$7C
	DB	$1F,$BC,$0F,$3C
	DB	$02,$3C,$00,$38
	DB	$00,$30,$00,$60
	DB	$01,$C0
LZ	DB	9,16,5
	DB	$18,$00,$3C,$80
	DB	$7F,$00,$9E,$00
	DB	$1E,$00,$1E,$00
	DB	$1E,$00,$1C,$00
	DB	$F0,$00,$1C,$00
	DB	$0E,$00,$0E,$00
	DB	$1E,$00,$1C,$00
	DB	$18,$00,$30,$00
	DB	$30,$00,$60,$80
	DB	$61,$80,$33,$00
	DB	$1E,$00

UA	DB	20,23,5
	DB	$0F,$00,$00,$1F,$80,$00

	DB	$33,$C0,$00,$41,$E0,$00
	DB	$41,$F0,$00,$24,$F0,$00
	DB	$19,$F8,$00,$01,$F8,$00
	DB	$02,$78,$00,$02,$7C,$00
	DB	$02,$7C,$00,$07,$FC,$00
	DB	$07,$FE,$00,$07,$FE,$00
	DB	$08,$1E,$00,$08,$1F,$00
	DB	$08,$1F,$00,$1E,$0F,$00
	DB	$3F,$AF,$90,$7F,$CF,$E0
	DB	$FF,$87,$C0,$CF,$07,$80
	DB	$86,$03,$00,$80,$00,$00
	DB	$80,$40,$00,$40,$C0,$00
	DB	$3F,$80,$00,$0E,$00,$00
UB	DB	17,23,5
	DB	$00,$06,$00,$00,$0F,$00
	DB	$00,$1F,$00,$00,$7F,$80
	DB	$01,$9F,$80,$1E,$4F,$80
	DB	$1E,$47,$00,$1E,$46,$00
	DB	$1E,$4C,$00,$7E,$50,$00
	DB	$7E,$78,$00,$FE,$4F,$00
	DB	$FE,$4F,$80,$9E,$4F,$80
	DB	$1E,$4F,$80,$18,$4F,$80
	DB	$10,$4F,$80,$1F,$CF,$80
	DB	$3F,$CF,$80,$3F,$EE,$00
	DB	$7F,$F0,$00,$61,$E0,$00
	DB	$40,$C0,$00,$80,$04,$00
	DB	$80,$08,$00,$40,$30,$00
	DB	$3F,$E0,$00,$0F,$80,$00
UD	DB	17,23,5
	DB	$40,$00,$00,$7F,$FE,$00
	DB	$7F,$FF,$00,$3F,$FF,$80
	DB	$3F,$FF,$80,$00,$CF,$80
	DB	$01,$4F,$80,$06,$4F,$80
	DB	$1E,$4F,$80,$7E,$4F,$80
	DB	$7E,$4F,$80,$FE,$4F,$80
	DB	$FE,$4F,$80,$9E,$4F,$80
	DB	$1E,$4F,$80,$18,$4F,$80
	DB	$10,$4F,$80,$1F,$CF,$80
	DB	$3F,$CF,$80,$3F,$EE,$00
	DB	$7F,$F0,$00,$61,$E0,$00
	DB	$40,$C0,$00,$80,$04,$00
	DB	$80,$08,$00,$40,$30,$00
	DB	$3F,$E0,$00,$0F,$80,$00
UE	DB	18,23,5
	DB	$40,$18,$00
	DB	$7E,$7C,$40
	DB	$7F,$FE,$40
	DB	$3F,$7F,$80
	DB	$3F,$27,$00
	DB	$0F,$20,$00
	DB	$0F,$20,$00
	DB	$0F,$20,$00
	DB	$0F,$20,$00
	DB	$0F,$3E,$00
	DB	$0F,$3E,$00
	DB	$0F,$3C,$00
	DB	$0F,$3C,$00
	DB	$0F,$20,$00
	DB	$0F,$20,$00
	DB	$0F,$20,$00
	DB	$0C,$20,$40
	DB	$08,$20,$40
	DB	$3C,$20,$80
	DB	$7F,$7F,$00
	DB	$7F,$FC,$00
	DB	$61,$F8,$00
	DB	$80,$70,$00
	DB	$80,$20,$00
	DB	$80,$08,$00
	DB	$40,$10,$00
	DB	$3F,$E0,$00
	DB	$0F,$80,$00
UF	DB	17,23,5
	DB	$00,$18,$80,$00,$3C,$80
	DB	$00,$FF,$00,$03,$FE,$00
	DB	$0F,$BC,$00,$0F,$98,$00
	DB	$0F,$90,$00,$0F,$90,$00
	DB	$7F,$9F,$00,$7F,$9F,$00
	DB	$FF,$9E,$00,$FF,$9E,$00
	DB	$0F,$90,$00,$0F,$90,$00
	DB	$0F,$90,$00,$00,$10,$00
	DB	$3C,$10,$00,$7E,$10,$00
	DB	$FF,$F0,$00,$FF,$F0,$00
	DB	$FF,$F0,$00,$8F,$E0,$00
	DB	$83,$C0,$00,$80,$00,$00
	DB	$80,$08,$00,$40,$10,$00
	DB	$3F,$E0,$00,$0F,$80,$00
UG	DB	22,23,0
	DB	$01,$81,$80,$06,$07,$C4
	DB	$0C,$1F,$E4,$18,$77,$F8
	DB	$39,$F2,$70,$3E,$F2,$00
	DB	$78,$F2,$00,$78,$F2,$00
	DB	$70,$F2,$00,$F0,$F2,$00
	DB	$F0,$F3,$E0,$F0,$F2,$78
	DB	$F0,$F2,$78,$F0,$F2,$7C
	DB	$70,$F2,$7C,$78,$F2,$78
	DB	$78,$C2,$78,$39,$02,$78
	DB	$3E,$02,$70,$1E,$02,$60
	DB	$0F,$02,$C0,$03,$C3,$80
	DB	$00,$FE,$00
UH	DB	17,22,5
	DB	$00,$30,$00,$00,$F9,$00
	DB	$03,$7E,$00,$0F,$3C,$00
	DB	$0F,$20,$00,$0F,$23,$00
	DB	$0F,$27,$80,$0F,$3F,$80
	DB	$0F,$2F,$80,$7F,$27,$80
	DB	$FF,$27,$80,$FF,$27,$80
	DB	$8F,$27,$80,$0F,$27,$80
	DB	$0F,$27,$80,$0C,$27,$80
	DB	$08,$27,$80,$1F,$27,$80
	DB	$3F,$A7,$80,$7F,$C7,$80
	DB	$C7,$87,$00,$83,$07,$00
	DB	$80,$27,$00,$40,$46,$00
	DB	$3F,$86,$00,$0E,$0C,$00
	DB	$00,$18,$00
UI
UJ
	DB	15,23,5
	DB	$0E,$00,$1F,$80
	DB	$3F,$C2,$47,$E4
	DB	$83,$F8,$81,$F4
	DB	$80,$EC,$48,$1C
	DB	$30,$BC,$00,$BC
	DB	$00,$BC,$07,$BC
	DB	$0F,$BC,$0F,$BC
	DB	$08,$BC,$00,$BC
	DB	$00,$BC,$00,$BC
	DB	$1C,$BE,$3E,$B0
	DB	$7F,$C0,$C7,$80
	DB	$83,$00,$80,$00
	DB	$80,$08,$40,$10
	DB	$3F,$E0,$0F,$80
UK	DB	20,22,4
	DB	$00,$30,$00,$00,$F9,$00
	DB	$03,$7E,$00,$0F,$3C,$00
	DB	$0F,$20,$00,$0F,$23,$00
	DB	$0F,$27,$80,$0F,$3F,$C0
	DB	$0F,$2F,$C0,$7F,$27,$C0
	DB	$FF,$27,$80,$FF,$27,$00
	DB	$8F,$26,$00,$0F,$2C,$00
	DB	$0F,$27,$00,$0C,$27,$80
	DB	$08,$27,$C0,$1F,$27,$C0
	DB	$3F,$A7,$F0,$7F,$C7,$E0
	DB	$C7,$83,$C0,$83,$01,$80
	DB	$80,$20,$00,$40,$40,$00
	DB	$3F,$80,$00,$0E,$00,$00
UL	DB	16,23,4
	DB	$00,$30,$00,$F9
	DB	$03,$7E,$0F,$3C
	DB	$0F,$20,$0F,$20
	DB	$0F,$20,$0F,$20
	DB	$0F,$20,$7F,$20
	DB	$FF,$20,$FF,$20
	DB	$8F,$20,$0F,$20
	DB	$0F,$20,$0C,$20
	DB	$08,$20,$1F,$F9
	DB	$3F,$FE,$7F,$FC
	DB	$FF,$F8,$C1,$F0
	DB	$80,$60,$80,$08
	DB	$40,$30,$3F,$E0
	DB	$0F,$80
UN	DB	19,23,4
	DB	$40,$02,$00,$7E,$07,$00
	DB	$7F,$0F,$80,$3F,$1F,$C0
	DB	$3F,$27,$80,$0F,$67,$80
	DB	$0F,$A7,$80,$0F,$27,$80
	DB	$0F,$27,$80,$0F,$27,$80
	DB	$3F,$27,$80,$7F,$27,$80
	DB	$7F,$27,$80,$4F,$27,$80
	DB	$0F,$27,$80,$0F,$27,$80
	DB	$0C,$27,$80,$08,$27,$80
	DB	$3C,$37,$80,$7F,$27,$A0
	DB	$7F,$C7,$C0,$8F,$83,$80
	DB	$83,$01,$00,$80,$20,$00
	DB	$40,$40,$00,$3F,$80,$00
	DB	$0E,$00,$00
UO	DB	22,23,0
	DB	$00,$C1,$80,$03,$07,$C0
	DB	$0E,$1F,$E0,$1C,$33,$F0
	DB	$3C,$F3,$F0,$39,$F2,$F8
	DB	$7A,$F2,$FC,$7C,$F2,$7C
	DB	$78,$F2,$7C,$F8,$F2,$7C
	DB	$F0,$F2,$7C,$F0,$F2,$3C
	DB	$F0,$F2,$3C,$F0,$F2,$3C
	DB	$F8,$F2,$3C,$F8,$F2,$38
	DB	$78,$F2,$78,$78,$C2,$70
	DB	$3F,$02,$60,$3C,$02,$C0
	DB	$1C,$02,$80,$0E,$03,$00
	DB	$03,$FE,$00
UP	DB	18,22,5
	DB	$40,$00,$00,$7E,$03,$00
	DB	$7F,$07,$80,$3F,$0F,$C0
	DB	$3F,$1F,$80,$0F,$27,$80
	DB	$0F,$67,$80,$0F,$A7,$80
	DB	$0F,$27,$80,$3F,$27,$80
	DB	$7F,$27,$80,$7F,$27,$80
	DB	$4F,$27,$80,$0F,$27,$80
	DB	$0F,$27,$80,$0F,$27,$80
	DB	$1F,$A7,$80,$3F,$E7,$00
	DB	$7F,$F4,$00,$CF,$F8,$00
	DB	$8F,$F0,$00,$8F,$64,$00
	DB	$8F,$08,$00,$6F,$30,$00
	DB	$3F,$E0,$00,$1F,$C0,$00
	DB	$0F,$00,$00
UQ	DB	23,23,5
	DB	$00,$C1,$80,$03,$07,$C0
	DB	$0E,$1F,$E0,$1C,$33,$F0
	DB	$3C,$F3,$F0,$39,$F2,$F8
	DB	$79,$F2,$FC,$7C,$F2,$7C
	DB	$78,$F2,$7C,$F8,$F2,$7C
	DB	$F0,$F2,$7C,$F0,$F2,$3C
	DB	$F0,$F2,$3C,$F0,$F2,$3C
	DB	$F8,$F2,$3C,$F8,$F2,$38
	DB	$78,$F2,$78,$78,$C2,$70
	DB	$3F,$02,$60,$3C,$02,$C0
	DB	$1C,$02,$80,$0E,$03,$00
	DB	$03,$FE,$02,$00,$0F,$E4
	DB	$00,$07,$F8,$00,$01,$F0
	DB	$00,$01,$E0,$00,$00,$C0
UR	DB	21,23,4
	DB	$40,$03,$00,$7E,$07,$80
	DB	$7F,$0F,$C0,$3F,$1F,$C0
	DB	$3F,$27,$E0,$0F,$63,$E0
	DB	$0F,$A1,$E0,$0F,$21,$C0
	DB	$0F,$23,$80
	DB	$0F,$23,$00,$3F,$26,$00
	DB	$7F,$24,$00,$7F,$3C,$00
	DB	$4F,$27,$00,$0F,$27,$80
	DB	$0F,$27,$C0,$0C,$27,$C0
	DB	$08,$27,$C0,$3C,$37,$C0
	DB	$7F,$27,$C8,$7F,$C7,$F0
	DB	$8F,$83,$E0,$83,$30,$C0
	DB	$80,$21,$80,$40,$40,$00
	DB	$3F,$80,$00,$0E,$00,$00
US	DB	18,24,4
	DB	$00,$C2,$00,$01,$E2,$00
	DB	$07,$FC,$00,$1D,$F8,$00
	DB	$7D,$F0,$00,$7C,$E0,$00
	DB	$7C,$80,$00,$7C,$80,$00
	DB	$7C,$88,$00,$7E,$9C,$00
	DB	$FF,$FE,$40,$7C,$BF,$80
	DB	$38,$9F,$00,$10,$9F,$00
	DB	$00,$9F,$00,$00,$9F,$00
	DB	$00,$9F,$00,$00,$9F,$00
	DB	$1C,$9F,$80,$3E,$9C,$00
	DB	$7F,$F0,$00,$C7,$E0,$00
	DB	$83,$C0,$00,$81,$80,$00
	DB	$80,$08,$00,$40,$10,$00
	DB	$3F,$E0,$00,$0F,$80,$00
UT	DB	22,25,0
	DB	$0E,$00,$00,$1F,$80,$04
	DB	$3F,$E0,$08,$7F,$FC,$38
	DB	$87,$FF,$F0,$01,$FF,$E0
	DB	$03,$3F,$C0,$0D,$0F,$80
	DB	$19,$14,$00,$39,$64,$00
	DB	$79,$E4,$00,$79,$E4,$00
	DB	$F1,$E4,$00,$F1,$E4,$00
	DB	$F1,$E4,$00,$F1,$E4,$00
	DB	$F1,$E4,$00,$79,$E4,$00
	DB	$79,$E4,$00,$39,$C4,$40
	DB	$39,$04,$40,$1D,$04,$80
	DB	$0D,$05,$00,$07,$8E,$00
	DB	$01,$FC,$00
UU	DB	19,23,4
	DB	$40,$20,$60,$7E,$23,$C0
	DB	$7F,$27,$80,$3F,$27,$80
	DB	$3F,$27,$80,$0F,$27,$80
	DB	$0F,$27,$80,$0F,$27,$80
	DB	$0F,$27,$80,$0F,$27,$80
	DB	$3F,$27,$80,$7F,$27,$80
	DB	$7F,$27,$80,$4F,$27,$80
	DB	$0F,$27,$80,$0F,$27,$80
	DB	$0C,$27,$80,$08,$2F,$80
	DB	$3C,$37,$80,$7F,$27,$A0
	DB	$7F,$C7,$C0,$8F,$83,$80
	DB	$83,$01,$00,$80,$20,$00
	DB	$40,$40,$00,$3F,$80,$00
	DB	$0E,$00,$00
UV	DB	18,23,4
	DB	$40,$02,$00,$7E,$07,$00
	DB	$7F,$0F,$80,$3F,$1F,$C0
	DB	$3F,$27,$80,$0F,$47,$80
	DB	$0F,$87,$80,$0F,$07,$80
	DB	$0F,$07,$80,$0F,$07,$80
	DB	$3F,$07,$80,$7F,$07,$80
	DB	$7F,$07,$80,$4F,$07,$80
	DB	$0F,$07,$80,$0F,$07,$80
	DB	$0C,$07,$80,$1E,$07,$80
	DB	$3F,$07,$80,$7F,$8F,$00
	DB	$7F,$FC,$00,$60,$F8,$00
	DB	$80,$60,$00,$80,$08,$00
	DB	$40,$10,$00,$3F,$E0,$00
	DB	$0F,$80,$00
UW	DB	24,23,4
	DB	$40,$08,$08,$7E,$1C,$1C
	DB	$7F,$3E,$3E,$3F,$7E,$7F
	DB	$3F,$5E,$9E,$0F,$9F,$1E
	DB	$0F,$1E,$1E,$0F,$1E,$1E
	DB	$0F,$1E,$1E,$0F,$1E,$1E
	DB	$3F,$1E,$1E,$7F,$1E,$1E
	DB	$7F,$1E,$1E,$4F,$1E,$1E
	DB	$0F,$1E,$1E,$0F,$1E,$1E
	DB	$0C,$1E,$1E,$08,$1E,$1E
	DB	$1F,$1E,$1E,$3F,$FF,$9C
	DB	$7F,$E7,$F0,$C1,$E3,$E0
	DB	$80,$C1,$80,$80,$08,$00
	DB	$40,$10,$00,$3F,$E0,$00
	DB	$0F,$80,$00
UX	DB	21,23,4
	DB	$40,$00,$00,$7F,$E1,$88
	DB	$7F,$F1,$C8,$3F,$F3,$F0
	DB	$3F,$F3,$E0,$01,$F4,$C0
	DB	$00,$F4,$00,$00,$F8,$00
	DB	$00,$F0,$00,$00,$F0,$00
	DB	$07,$FF,$00,$07,$FE,$00
	DB	$0F,$FE,$00,$00,$F0,$00
	DB	$00,$F0,$00,$00,$F0,$00
	DB	$01,$F8,$00,$01,$F8,$20
	DB	$3A,$FC,$40,$7E,$7F,$C0
	DB	$7C,$7F,$80,$FC,$3F,$80
	DB	$88,$1F,$00,$80,$8E,$00
	DB	$41,$00,$00,$3E,$00,$00
	DB	$1C,$00,$00
UY	DB	18,23,5
	DB	$40,$02,$00,$7E,$07,$00
	DB	$7F,$0F,$80,$3F,$1F,$C0
	DB	$3F,$27,$80,$0F,$67,$80
	DB	$0F,$A7,$80,$0F,$27,$80
	DB	$0F,$27,$80,$0F,$27,$80
	DB	$3F,$27,$80,$7F,$27,$80
	DB	$7F,$27,$80,$4F,$27,$80
	DB	$0F,$27,$80,$0F,$27,$80
	DB	$0C,$27,$80,$08,$27,$80
	DB	$3C,$37,$80,$7F,$27,$80
	DB	$7F,$C7,$80,$8F,$87,$80
	DB	$83,$07,$80,$80,$27,$80
	DB	$40,$47,$00,$3F,$86,$00
	DB	$0E,$0C,$00,$00,$30,$00
UZ	DB	14,23,5
	DB	$40,$00,$7F,$E0
	DB	$7F,$F0,$7F,$F8
	DB	$3F,$F8,$04,$FC
	DB	$04,$7C,$04,$7C
	DB	$04,$38,$04,$30
	DB	$04,$60,$1F,$80
	DB	$04,$78,$04,$7C
	DB	$04,$7C,$04,$7C
	DB	$04,$7C,$1C,$7C
	DB	$3E,$7C,$7F,$70
	DB	$FF,$80,$CF,$00
	DB	$86,$00,$80,$00
	DB	$80,$40,$40,$C0
	DB	$3F,$80,$0E,$00
UM	DB	24,23,3
	DB	$40,$08,$10,$7E,$1C,$38
	DB	$7F,$3E,$7C,$3F,$3E,$7E
	DB	$3F,$5E,$BC,$0F,$5E,$BC
	DB	$0F,$9F,$3C,$0F,$1E,$3C
	DB	$0F,$1E,$3C,$0F,$1E,$3C
	DB	$3F,$1E,$3C,$7F,$1E,$3C
	DB	$7F,$1E,$3C,$4F,$1E,$3C
	DB	$0F,$1E,$3C,$0F,$1E,$3C
	DB	$0C,$1E,$3C,$08,$1E,$3C
	DB	$3C,$1E,$3C,$7E,$5E,$BD
	DB	$FF,$DF,$3E,$9F,$8E,$1C
	DB	$8F,$24,$08,$40,$40,$00
	DB	$3F,$80,$00,$0E,$00,$00
ZERO	DB	14,16,0
	DB	0,$F0,$1,$F8
	DB	7,$0C,$0E,$0C
	DB	$1C,$0C,$3C,$0C
	DB	$78,$1C,$78,$18
	DB	$F0,$38,$F0,$30
	DB	$E0,$30,$C0,$60
	DB	$C0,$40,$41,$80
	DB	$46,$0,$38,0
ONE	DB	13,16,0
	DB	0,$38,0,$58
	DB	$0,$90,$1,$30
	DB	$02,$30,$4,$60
	DB	$8,$E0,$10,$C0
	DB	$21,$C0,$43,$80
	DB	$83,$80,$07,$80
	DB	$0F,$0,$1E,$0
	DB	$1E,$0,$3C,$0
TWO	DB	15,16,0
	DB	$01,$F8,$3,$9C
	DB	4,$0E,$08,$0E
	DB	$11,$1E,$12,$3C
	DB	$0C,$3C,$0,$78
	DB	$0,$E0,$01,$C0
	DB	$07,$0,$0E,$0
	DB	$18,$0,$78,$10
	DB	$67,$E0,$C3,$C0
THREE	DB	14,17,0
	DB	0,$F0,$3,$F8
	DB	06,$0C,$08,$1C
	DB	$10,$1C,$12,$1C
	DB	$0C,$38,0,$70
	DB	3,$E0,$01,$C0
	DB	0,$E0,$0,$F0
	DB	$60,$F0,$E1,$E0
	DB	$83,$C0,$87,$80
	DB	$7F,$0
FOUR	DB	14,16,2
	DB	0,$F0,0,$E0
	DB	1,$C0,$03,$C0
	DB	$03,$84,$07,$08
	DB	06,$18,$0C,$30
	DB	$18,$70,$30,$E0
	DB	$71,$C0,$DF,$C8
	DB	$03,$F0,$03,$80
	DB	$07,$0,$07,$0
	DB	$0F,$0,$0F,0
FIVE	DB	15,18,0
	DB	0,6,$0C,$0C
	DB	$0F,$FC,$0F,$F8
	DB	$0E,$0,$0E,$0
	DB	$0C,$0,$0C,$0
	DB	$0F,$C0,$1C,$60
	DB	0,$60,$0,$70
	DB	$60,$78,$F0,$F8
	DB	$F0,$F8,$E1,$F0
	DB	$43,$E0,$3F,$80
SIX	DB	14,17,0
	DB	0,$70,1,$C8
	DB	7,$8C,$0E,$1C
	DB	$1C,$18,$30,0
	DB	$30,0,$33,$80
	DB	$74,$C0,$E8,$C0
	DB	$F0,$E0,$F0,$E0
	DB	$E0,$E0,$E1,$C0
	DB	$C3,$C0,$67,$80
	DB	$3F,0
SEVEN	DB	15,17,2
	DB	3,$C6,$0F,$FC
	DB	$1F,$FC,$33,$38
	DB	$60,$30,$60,$60
	DB	0,$E0,$01,$C0
	DB	3,$80,$7,0
	DB	$0E,0,$1E,$0
	DB	$1C,0,$3C,0
	DB	$38,$0,$78,0
	DB	$F0,$0,$F0,0
	DB	$F0,0
EIGHT	DB	14,17,0
	DB	0,$F0,$03,$0C
	DB	6,$04,$06,$04
	DB	$0F,$0C,$0F,$0C
	DB	$07,$18,$07,$A0
	DB	$0F,$C0,$33,$E0
	DB	$61,$F0,$C0,$F0
	DB	$C0,$70,$C0,$70
	DB	$C0,$60,$61,$C0
	DB	$3F,$80
NINE	DB	15,16,0
	DB	01,$F8,3,$0C
	DB	6,$0C,$0C,$0E
	DB	$0C,$1E,$1C,$1E
	DB	$1C,$3E,$0C,$7C
	DB	$07,$BC,$0,$38
	DB	0,$70,0,$E0
	DB	$C1,$C0,$C3,$80
	DB	$C7,0,$7E,0

;CAMELOT SWORD ROUTINE V14 JUNE 22, 1982
;
; WITH MUSIC
;
RTCLKL	=	$14
COLRSH	=	$4F
DRKMSK	=	$4E
;
;
	ORG	$B0
SCNPTR	ORG	*+2
DLPTR	ORG	*+2
OLDBEG	ORG	*+1
OLDEND	ORG	*+1
NEWBEG	ORG	*+1
NEWEND	ORG	*+1
TEMP1	ORG	*+1
TEMP2	ORG	*+1
;
	ORG	$3600
	JMP	BEGIN
;
;
STEP	ORG	*+1
BASEX	ORG	*+1
BASEY	ORG	*+1
DELTAX	ORG	*+1
DELTAY	ORG	*+1
TEMP	ORG	*+1
MEMORG	ORG	*+1
ROTCNT	ORG	*+1
HSLCNT	ORG	*+1
VSLCNT	ORG	*+1
SHSCRL	ORG	*+1
FMCNTL	ORG	*+1
P0BEG	ORG	*+1
P1BEG	ORG	*+1
P2BEG	ORG	*+1
P3BEG	ORG	*+1
MBEG	ORG	*+1
P0END	ORG	*+1
P1END	ORG	*+1
P2END	ORG	*+1
P3END	ORG	*+1
MEND	ORG	*+1
MTNCNT	ORG	*+1
LEFT	ORG	*+1
RIGHT	ORG	*+1
COLOR	ORG	*+1
CHRIS	ORG	*+1
CHRIS2	ORG	*+1
SUM	ORG	*+1
SUM2	ORG	*+1
STARTX	ORG	*+1
RMASK	ORG	*+1
YCNT	ORG	*+1
CLDCNT	ORG	*+1
HNDCNT	ORG	*+1
HNDSTP	ORG	*+1
FMCNT2	ORG	*+1
DLICNT	ORG	*+1
DL1PF0	ORG	*+1
DL2PF0	ORG	*+1
DL2BAK	ORG	*+1
DL1LOC	ORG	*+1
DL2LOC	ORG	*+1
SMASK	ORG	*+1
CENTER	ORG	*+1
SNDCNT	ORG	*+1
ROLLER	ORG	*+1
DL1PF1	ORG	*+1
DL1PF2	ORG	*+1
;
LCNT	DB	0
JFCNT	DB	0
JFCNT1	DB	0
NTCNT1	DB	0
NTCNT2	DB	0
NTCNT3	DB	0
TIM1	DB	0
TIM2	DB	0
TIM3	DB	0
AUDC1S	DB	0
AUDC2S	DB	0
;
POWRUP	=	$E477
DOSVEC	=	$0A
DOSINI	=	$0C
VKEYBD	=	$208
DOSXIT	=	$179F
DLIVEC	=	$200
DLISTL	=	$230
DLISTH	=	$231
SDMCTL	=	$22F
GPRIOR	=	$26F
PRIOR	=	$D01B
PCOLR0	=	$2C0
PCOLR1	=	$2C1
PCOLR2	=	$2C2
PCOLR3	=	$2C3
COLOR0	=	$2C4
COLOR1	=	$2C5
COLOR2	=	$2C6
COLOR3	=	$2C7
COLOR4	=	$2C8
M	=	$3180
P0	=	M+128
P1	=	P0+128
P2	=	P1+128
P3	=	P2+128
HPOSP0	=	$D000
HPOSP1	=	$D001
HPOSP2	=	$D002
HPOSP3	=	$D003
HPOSM0	=	$D004
HPOSM1	=	$D005
HPOSM2	=	$D006
HPOSM3	=	$D007
COLPM3	=	$D015
COLPF0	=	$D016
COLPF1	=	$D017
COLPF2	=	$D018
COLPF3	=	$D019
COLBAK	=	$D01A
SIZEP0	=	$D008
SIZEP1	=	$D009
SIZEP2	=	$D00A
SIZEP3	=	$D00B
SIZEM	=	$D00C
TRIG0	=	$D010
RANDOM	=	$D20A
PMBASE	=	$D407
DMACTL	=	$D400
HSCROL	=	$D404
VSCROL	=	$D405
WSYNC	=	$D40A
VCOUNT	=	$D40B
NMIEN	=	$D40E
GRACTL	=	$D01D
SETVBV	=	$E45C
XITVBV	=	$E462
VBIOLD	=	$E45F
AUDF1  = $D200
AUDF2  = $D202
AUDF3  = $D204
AUDC1  = $D201
AUDC2  = $D203
AUDC3  = $D205
AUDCTL = $D208
SKCTL  = $D20F
SSKCTL = $0232
TEMPO  = 4
;
RTSWD:  MACRO   PLAYER,MISSILE
	PROC
	LDX	STEP
	LDA	BASEX
	CLC
	ADC	%1XOFF,X
	IF	%2
	MISSL
	ELSE
	STA	HPOS%1
	ENDIF
	LDA	BASEY
	CLC
	ADC	%1YOFF,X
	TAY
	LDA	%1TOFF,X
	TAX
	STY	%1BEG
:LOOP1	INX
	INY
	LDA	%1TAB-1,X
	STA	%1,Y
	BNE	:LOOP1
	STY	%1END
	EPROC
	ENDM
;
;now reverse the table
;
REVRS:	MACRO	PLAYER
	PROC
	LDA	STEP
	CMP	#4
	BEQ	:NFLIP
	CMP	#$C
	BEQ	:NFLIP
	LDY	STEP
	LDX	%1TOFF,Y
	DEX
:LOOP2	INX
	LDA	%1TAB,X
	PHA
	BNE	:LOOP2
	PLA
	STX	TEMP
	LDX	%1TOFF,Y
	CPX	TEMP
	BEQ	:NFLIP
:LOOP3	PLA
	STA	%1TAB,X
	INX
	CPX	TEMP
	BNE	:LOOP3
:NFLIP	NOP
	EPROC
	ENDM
;
;macro to handle missiles
;
MISSL:	MACRO
	STA	HPOSM3
	CLC
	ADC	#$02
	STA	HPOSM2
	CLC
	ADC	#2
	STA	HPOSM1
	CLC
	ADC	#2
	STA	HPOSM0
	ENDM
;
;macro to clear players
;
CLRPL:	MACRO	PLAYER
	PROC
	LDA	#0
	LDX	%1BEG
:LOOPA	STA	%1,X
	INX
	CPX	%1END
	BNE	:LOOPA
	EPROC
	ENDM
;
RETRC:	MACRO	PLAYER
	PROC
	LDX	%1END
:LOOPB	LDA	%1-1,X
	STA	%1,X
	DEX
	CPX	%1BEG-1
	BNE	:LOOPB
	LDA	%1END
	CMP	#$6F
	BCS	:X1
	INC	%1END
:X1	NOP
	EPROC
	ENDM
;
;
;
;
BEGIN	CLD
	LDA	#$30
	STA	PMBASE
	LDA	#0
	STA	SDMCTL	;turn off ANTIC for starters
	STA	DMACTL
	LDA	#3
	STA	GRACTL
	LDA	#$11
	STA	PRIOR
	STA	GPRIOR
	LDA	#$70
	STA	MEMORG
	LDA	#0
	STA	AUDCTL
	LDA	#3
	STA	SKCTL
;
;clear out player RAM
;
	LDA	#0
	TAX
LOOP4	STA	M,X
	INX
	BNE	LOOP4
LOOP5	STA	P1,X
	INX
	BNE	LOOP5
LOOP6	STA	P2,X
	INX
	BNE	LOOP6
;
;initialize colors
;
	LDA	#$8F
	STA	PCOLR0
	STA	PCOLR1
	STA	PCOLR2
	STA	PCOLR3
	LDA	#$88
	STA	COLOR4
	LDA	#$0F
	STA	COLOR0
	LDA	#$12
	STA	COLOR1
	LDA	#$D4
	STA	COLOR2
	LDA	#$46
	STA	COLOR3
	LDA	#0
	STA	SIZEP0
	STA	SIZEP1
	STA	SIZEP2
	STA	SIZEP3
	STA	SIZEM
	STA	STEP
	STA	DELTAX
	STA	DELTAY
	STA	SHSCRL
	STA	HSCROL
	STA	VSCROL
	STA	ROTCNT
	STA	HSLCNT
	STA	VSLCNT
	STA	FMCNTL
	STA	HNDCNT
	STA	FMCNT2
	STA	DL1PF0
	STA	DLICNT
	LDA	#$4E
	STA	DL2PF0
	LDA	#$94
	STA	DL2BAK
	LDA	#138
	STA	DL1LOC
	LDA	#237
	STA	DL2LOC
	LDA	#1
	STA	HNDSTP
	LDA	#$10
	STA	BASEX
	STA	BASEY
	LDA	#$20
	STA	SMASK
	LDA	#$10
	STA	CENTER
;
;write display list
;
	LDA	MEMORG
	STA	SCNPTR+1
	CLC
	ADC	#32
	STA	DLPTR+1
	STA	DLISTH
	LDA	#0
	STA	SCNPTR
	STA	DLPTR
	STA	DLISTL
;
	LDY	#0
	LDX	#0
	LDA	#$70
	STA	(DLPTR),Y
	INY
	STA	(DLPTR),Y
	INY
	STA	(DLPTR),Y
	INY
LOOP14	LDA	#$7D
	STA	(DLPTR),Y
	INY
	LDA	SCNPTR
	STA	(DLPTR),Y
	INY
	LDA	SCNPTR+1
	STA	(DLPTR),Y
	INY
	INX
	CPX	#84
	BNE	X2
	STY	DLPTR
	LDY	#0
X2	LDA	SCNPTR
	CLC
	ADC	#$40
	STA	SCNPTR
	BCC	X3
	INC	SCNPTR+1
X3	CPX	#96
	BNE	LOOP14
	LDA	#$41
	STA	(DLPTR),Y
	INY
	LDA	DLPTR
	STA	(DLPTR),Y
	INY
	LDA	DLPTR+1
	STA	(DLPTR),Y
;
;routine to clear screen
;
	LDA	MEMORG
	STA	SCNPTR+1
	LDA	#0
	STA	SCNPTR
	TAX
	TAY
LOOP13	STA	(SCNPTR),Y
	INY
	BNE	LOOP13
	INC	SCNPTR+1
	INX
	CPX	#16
	BNE	X30
	LDA	#$FF
X30	CPX	#32
	BNE	LOOP13
;
;initialize mountains
;
	LDA	#0
	STA	MTNCNT
;
LOOP12	LDA	MTNCNT
	LSR	A
	LSR	A
	LSR	A
	CLC
	ADC	#$10
	ADC	MEMORG
	STA	SCNPTR+1
	LDA	MTNCNT
	AND	#6
	ROR	A
	ROR	A
	ROR	A
	ROR	A
	STA	SCNPTR
;
	LDA	#$40
	STA	CHRIS
	STA	CHRIS2
	LDA	#$60
	STA	SUM
	STA	SUM2
;
X26	LDX	RANDOM
	CPX	#$E8
	BCS	X26
	STX	OLDBEG
	INX
	STX	NEWBEG
	LDA	RANDOM
	AND	#$F
	CLC
	ADC	#$10
	ADC	OLDBEG
	BCS	X25
	CMP	#$FE
	BCC	X14
X25	LDA	#$FE
X14	STA	OLDEND
	SEC
	SBC	#1
	STA	NEWEND
;
LOOP11	LDA	#1
	STA	COLOR
	LDA	OLDBEG
	STA	LEFT
	LDA	OLDEND
	STA	RIGHT
	JSR	HLINE
	LDA	NEWBEG
	CLC
	ADC	#2
	CMP	NEWEND
	BCS	X15
	LDA	NEWBEG
	STA	LEFT
	LDA	NEWEND
	STA	RIGHT
	LDA	#2
	STA	COLOR
	JSR	HLINE
;
;next level
;
	LDA	SCNPTR
	SEC
	SBC	#$40
	STA	SCNPTR
	BCS	X16
	DEC	SCNPTR+1
X16	LDA	NEWBEG
	STA	OLDBEG
	LDA	NEWEND
	STA	OLDEND
;
	INC	NEWBEG
	LDA	CHRIS
LOOP10	CLC
	ADC	SUM
	STA	SUM
	BCC	X17
	INC	NEWBEG
	LDA	RANDOM
	AND	#$1F
	CLC
	ADC	CHRIS
	BCC	X18
	LDA	#$FF
X18	STA	CHRIS
	JMP	LOOP10
X17	DEC	NEWEND
	LDA	CHRIS2
X19	CLC
	ADC	SUM2
	STA	SUM2
	BCS	X20
	DEC	NEWEND
	LDA	RANDOM
	AND	#$1F
	CLC
	ADC	CHRIS2
	BCC	X21
	LDA	#$FF
X21	STA	CHRIS2
	JMP	X19
;
X20	JMP	LOOP11
;
X15	INC	MTNCNT
	LDA	MTNCNT
	CMP	#30
	BCS	X22
	JMP	LOOP12
;
;calling routine to draw clouds
;
X22	LDA	#1
	STA	COLOR
	LDY	#3
	STY	RMASK
	STY	CLDCNT
LOOP19	LDA	MEMORG
	CLC
	ADC	#8
	STA	SCNPTR+1
	LDA	#0
	STA	SCNPTR
	LDX	BASET,Y
	LDY	#0
	JSR	LOOP18
	DEC	CLDCNT
	LDY	CLDCNT
	BPL	LOOP19
;
;draw lake
;
	LDA	#0
	STA	COLOR
	LDA	MEMORG
	CLC
	ADC	#20
	STA	SCNPTR+1
	LDA	#0
	STA	SCNPTR
	LDA	#7
	STA	RMASK
	LDX	#$55
	LDY	#14
	JSR	LOOP18
;
;enable VBI
;
	LDX	#VBISRM/256
	LDY	#VBISRM&$FF
	LDA	#7
	JSR	SETVBV
;
;set up DLI's
;
	LDA	MEMORG
	CLC
	ADC	#32
	STA	DLPTR+1
	LDA	#0
	STA	DLPTR
	LDY	DL1LOC
	LDA	#$FD
	STA	(DLPTR),Y
	LDY	DL2LOC
	STA	(DLPTR),Y
	LDA	#DLISRV/256
	STA	DLIVEC+1
	LDA	#DLISRV&255
	STA	DLIVEC
X55	LDA	VCOUNT
	CMP	#16
	BCS	X55
	LDA	#$C0
	STA	NMIEN
	LDA	#$2F
	STA	SDMCTL
	STA	DMACTL
;
	LDY	#255
KKKK00:
	LDX	#255
KKKK01:
	PHA
	PLA
	PHA
	PLA
	PHA
	PLA
	PHA
	PLA
	PHA
	PLA
	PHA
	PLA
	PHA
	PLA
	PHA
	PLA
	PHA
	PLA
	PHA
	PLA
	PHA
	PLA
	PHA
	PLA
	PHA
	PLA
	DEX
	BNE	KKKK01
	DEY
	BNE	KKKK00
;
;enable other VBI
;
	LDX	#VBISR/256
	LDY	#VBISR&$FF
	LDA	#7
	JSR	SETVBV
;
	LDA	#$C0
	STA	NMIEN
	LDA	#$2F
	STA	SDMCTL
	STA	DMACTL
;
HANG
	LDX	#255
	JSR	WAYT
	LDX	#255
	JSR	WAYT
;
	LDX	#VBIOLD/256
	LDY	#VBIOLD&255
	LDA	#6
	JSR	SETVBV
;
	LDX	#XITVBV/256
	LDY	#XITVBV&$FF
	LDA	#7
	JSR	SETVBV
;
	LDA	#$C0
	STA	NMIEN
	LDA	#$22
	STA	DMACTL
	LDA	#00
	STA	HPOSP0
	STA	HPOSP1
	STA	HPOSP2
	STA	HPOSP3
	STA	HPOSM0
	STA	HPOSM1
	STA	HPOSM2
	STA	HPOSM3
;
	JSR	DIMALL
;
HEREZ:
	JMP	HEREZ		; HANG FOREVER
;
;
	;	JMP	POWRUP
;
	;	LDA	#$9F		; PREPARE TO GET OUT
	;	STA	DOSVEC		; THIS CODE DUPLICATED
	;	LDA	#$17		; FROM $3731, WHERE
	;	STA	DOSVEC+1	; DOSVEC USUALLY POINTS
	;	LDA	#$40		; BUT CODE WAS DESTROYED
	;	STA	DOSINI		; BY ENDING.OBJ AND
	;	LDA	#$15		; CAN NOT RELOCATE ENDING
	;	STA	DOSINI+1	; SOMEWHERE ELSE EASILY
	;	LDA	#$BE
	;	STA	VKEYBD
	;	LDA	#$FF
	;	STA	VKEYBD+1
	;	JMP	DOSXIT
;
;
;
DIMALL:
	LDX	#$00
DIM00:
	STX	DIMX
;
	LDA	Q0,X
	STA	COLOR0
	STA	COLOR1
;
	LDA	Q2,X
	STA	COLOR2
	STA	COLOR4
;
	;	LDA	Q3,X
	;	STA	COLOR3
;
	LDA	DL1P0,X
	STA	DL1PF0
	LDA	DL1P1,X
	STA	DL1PF1
	LDA	DL1P2,X
	STA	DL1PF2
;
	LDA	DL2P0,X
	STA	DL2PF0
	LDA	DL2BK,X
	STA	DL2BAK
;
	LDX	#01
	JSR	WAYT
;
	LDX	DIMX
	INX
	CPX	#15
	BCC	DIM00
;
	RTS
;
DIMX:	DB	0
;
;
Q0:
	DB	$0D,$0C,$0B,$0A
	DB	$09,$08,$07,$06
	DB	$05,$04,$03,$02
	DB	$01,$00,$00
Q2:
	DB	$88,$88,$88,$88
	DB	$88,$88,$87,$86
	DB	$85,$84,$83,$82
	DB	$81,$80,$00
Q3:
	DB	$46,$46,$46,$46
	DB	$46,$46,$46,$46
	DB	$45,$44,$43,$42
	DB	$41,$40,$00
DL1P0:
	DB	$00,$00,$00,$00
	DB	$00,$00,$00,$00
	DB	$00,$00,$00,$00
	DB	$00,$00,$00
DL1P1:
	DB	$12,$12,$12,$12
	DB	$12,$12,$12,$12
	DB	$12,$12,$12,$12
	DB	$11,$10,$00
DL1P2:
	DB	$D4,$D4,$D4,$D4
	DB	$D4,$D4,$D4,$D4
	DB	$D4,$D4,$D3,$D2
	DB	$D1,$D0,$00
DL2P0:
	DB	$4D,$4C,$4B,$4A
	DB	$49,$48,$47,$46
	DB	$45,$44,$43,$42
	DB	$41,$40,$00
DL2BK:
	DB	$94,$94,$94,$94
	DB	$94,$94,$94,$94
	DB	$94,$94,$93,$92
	DB	$91,$90,$00
;
;
;
WAYT:
	LDY	#255
WAYT0:
	ROL	ROLLER
	ROR	ROLLER
	ROL	ROLLER
	ROR	ROLLER
	ROL	ROLLER
	ROR	ROLLER
	ROL	ROLLER
	ROR	ROLLER
	ROL	ROLLER
	ROR	ROLLER
	ROL	ROLLER
	ROR	ROLLER
	ROL	ROLLER
	ROR	ROLLER
	ROL	ROLLER
	ROR	ROLLER
	ROL	ROLLER
	ROR	ROLLER
	ROL	ROLLER
	ROR	ROLLER
	DEY
	BNE	WAYT0
	DEX
	BNE	WAYT
	RTS
;
;
;subroutine to draw from a template
;
LOOP18	STY	YCNT
	STX	STARTX
	LDA	RANDOM
	AND	RMASK
	CLC
	ADC	TMPLTL,Y
	ADC	STARTX
	STA	LEFT
	LDA	RANDOM
	AND	RMASK
	CLC
	ADC	TMPLTR,Y
	ADC	STARTX
	BCS	X28
	CMP	#$FF	;special case test
	BNE	X24
X28	LDA	#$FE
X24	STA	RIGHT
	JSR	HLINE
	LDA	SCNPTR
	CLC
	ADC	#$40
	STA	SCNPTR
	BCC	X31
	INC	SCNPTR+1
X31	LDY	YCNT
	INY
	LDX	STARTX
	LDA	TMPLTR,Y
	BNE	LOOP18
	RTS
;
;subroutines to draw lines
;
PLOT	PHA
	LSR	A
	LSR	A
	TAY
	PLA
	AND	#3
	ASL	A
	ADC	#3
	TAX
	STA	TEMP1
	LDA	(SCNPTR),Y
LOOP9	ROL	A
	DEX
	BNE	LOOP9
	STA	TEMP2
	LDA	COLOR
	EOR	TEMP2
	AND	#3
	EOR	TEMP2
	LDX	TEMP1
LOOP8	ROR	A
	DEX
	BNE	LOOP8
	STA	(SCNPTR),Y
	RTS
;
HLINE	LDA	LEFT
	JSR	PLOT
	INC	LEFT
	LDA	RIGHT
	CMP	LEFT
	BCS	HLINE
	RTS
;
;DLI service routine
;
DLISRV	PHA
	INC	DLICNT
	LDA	DLICNT
	LSR	A
	BCC	DLI2
	LDA	DL1PF0
	EOR	COLRSH
	AND	DRKMSK
	STA	COLPF0
	PLA
	RTI
DLI2	LDA	DL2PF0
	EOR	COLRSH
	AND	DRKMSK
	STA	WSYNC
	STA	COLPF0
	LDA	DL2BAK
	EOR	COLRSH
	AND	DRKMSK
	STA	COLBAK
	PLA
	RTI
;
;vertical blank interrupt service routine(S)
;
;
VBISRM:
	INC	JFCNT
	BNE	M0
	INC	JFCNT+1
;
M0:
	LDA	JFCNT
	CMP	#128
	BNE	M1
	LDA	JFCNT+1
	CMP	#5
	BNE	M1
;
	LDA	#0
	STA	JFCNT
	STA	JFCNT+1
	STA	NTCNT1
	STA	NTCNT2
	STA	NTCNT3
	STA	TIM1
	STA	TIM2
	STA	TIM3
	JMP	XITVBV
;
M1:			; VOICE 1
	INC	TIM1
	LDX	NTCNT1
;
	LDA	T1,X	; DECAY ON VOICE 1
	SEC
	SBC	C1,X
	CMP	TIM1
	BCS	M1A
	LDA	AUDC1S
	CMP	#$E1
	BEQ	M1A
	LDA	AUDC1S
	SEC
	SBC	#1
	STA	AUDC1
	STA	AUDC1S
;
M1A:
	LDA	T1,X
	CMP	TIM1
	BNE	M1C
	LDA	#1
	STA	TIM1
	INC	NTCNT1
	INX
	LDA	#$E1
	STA	AUDC1
	STA	AUDC1S
	LDA	F1,X
	STA	AUDF1
	JMP	M2
;
M1C:			; ATTACK ON VOICE 1
	LDA	#$11
	CMP	TIM1
	BCC	M2
	LDA	C1,X
	ORA	#$E0
	CMP	AUDC1S
	BEQ	M2
;
	LDA	AUDC1S
	CLC
	ADC	#1
	STA	AUDC1
	STA	AUDC1S
;
M2:			; VOICE 2
	INC	TIM2
	LDX	NTCNT2
;
	LDA	T2,X	; DECAY ON VOICE 2
	SEC
	SBC	C2,X
	CMP	TIM2
	BCS	M2A
	LDA	AUDC2S
	CMP	$E1
	BEQ	M2A
	LDA	AUDC2S
	SEC
	SBC	#1
	STA	AUDC2
	STA	AUDC2S
;
M2A:
	LDA	T2,X
	CMP	TIM2
	BNE	M2C
	LDA	#1
	STA	TIM2
	INC	NTCNT2
	INX
	LDA	#$E1
	STA	AUDC2
	STA	AUDC2S
	LDA	F2,X
	STA	AUDF2
	JMP	M3
;
M2C:			; ATTACK ON VOICE 2
	LDA	#$11
	CMP	TIM2
	BCC	M3
	LDA	C2,X
	ORA	#$E0
	CMP	AUDC2S
	BEQ	M3
;
	LDA	AUDC2S
	CLC
	ADC	#1
	STA	AUDC2
	STA	AUDC2S
;
M3:
	INC	TIM3
	LDX	NTCNT3
	LDA	T3,X
	CMP	TIM3
	BNE	M4
	LDA	#1
	STA	TIM3
	INC	NTCNT3
	INX
	LDA	F3,X
	BNE	M3C
	LDA	#$E1
	STA	AUDC3
	JMP	M3D
M3C:
	LDA	#$E3
	STA	AUDC3
M3D:
	LDA	F3,X
	STA	AUDF3
;
M4:
	JMP	XITVBV
;
; **********************************
VBISR
	INC	JFCNT
	BNE	L0
	INC	JFCNT+1
;
L0:
	LDA	JFCNT
	CMP	#128
	BNE	L1
	LDA	JFCNT+1
	CMP	#5
	BNE	L1
;
	LDA	#0
	STA	JFCNT
	STA	JFCNT+1
	STA	NTCNT1
	STA	NTCNT2
	STA	NTCNT3
	STA	TIM1
	STA	TIM2
	STA	TIM3
	JMP	XITVBV
;
L1:			; VOICE 1
	INC	TIM1
	LDX	NTCNT1
;
	LDA	T1,X	; DECAY ON VOICE 1
	SEC
	SBC	C1,X
	CMP	TIM1
	BCS	L1A
	LDA	AUDC1S
	CMP	#$E1
	BEQ	L1A
	LDA	AUDC1S
	SEC
	SBC	#1
	STA	AUDC1
	STA	AUDC1S
;
L1A:
	LDA	T1,X
	CMP	TIM1
	BNE	L1C
	LDA	#1
	STA	TIM1
	INC	NTCNT1
	INX
	LDA	#$E1
	STA	AUDC1
	STA	AUDC1S
	LDA	F1,X
	STA	AUDF1
	JMP	L2
;
L1C:			; ATTACK ON VOICE 1
	LDA	#$11
	CMP	TIM1
	BCC	L2
	LDA	C1,X
	ORA	#$E0
	CMP	AUDC1S
	BEQ	L2
;
	LDA	AUDC1S
	CLC
	ADC	#1
	STA	AUDC1
	STA	AUDC1S
;
L2:			; VOICE 2
	INC	TIM2
	LDX	NTCNT2
;
	LDA	T2,X	; DECAY ON VOICE 2
	SEC
	SBC	C2,X
	CMP	TIM2
	BCS	L2A
	LDA	AUDC2S
	CMP	$E1
	BEQ	L2A
	LDA	AUDC2S
	SEC
	SBC	#1
	STA	AUDC2
	STA	AUDC2S
;
L2A:
	LDA	T2,X
	CMP	TIM2
	BNE	L2C
	LDA	#1
	STA	TIM2
	INC	NTCNT2
	INX
	LDA	#$E1
	STA	AUDC2
	STA	AUDC2S
	LDA	F2,X
	STA	AUDF2
	JMP	L3
;
L2C:			; ATTACK ON VOICE 2
	LDA	#$11
	CMP	TIM2
	BCC	L3
	LDA	C2,X
	ORA	#$E0
	CMP	AUDC2S
	BEQ	L3
;
	LDA	AUDC2S
	CLC
	ADC	#1
	STA	AUDC2
	STA	AUDC2S
;
L3:
	INC	TIM3
	LDX	NTCNT3
	LDA	T3,X
	CMP	TIM3
	BNE	L4
	LDA	#1
	STA	TIM3
	INC	NTCNT3
	INX
	LDA	F3,X
	BNE	L3C
	LDA	#$E1
	STA	AUDC3
L3C:
	LDA	#$E3
	STA	AUDC3
L3D:
	LDA	F3,X
	STA	AUDF3
;
L4:
	INC	FMCNTL
	LDA	FMCNTL
	CMP	#$F2
	BNE	X41
	DEC	FMCNTL
	JMP	STAGE2
X41	INC	ROTCNT
	LDA	ROTCNT
	CMP	#1
	BNE	X1
	JMP	ROTSWD
X1	CMP	#2
	BNE	X40
	JMP	REVERS
X40	CMP	#5
	BNE	NXTISR
	LDA	#0
	STA	ROTCNT
;
NXTISR	LDA	MEMORG
	CLC
	ADC	#32
	STA	DLPTR+1
	LDA	#0
	STA	DLPTR
	INC	VSLCNT
	LDY	#0
	LDA	VSLCNT
	CMP	#4
	BNE	X47
	LDX	#1
	JMP	X48
X47	CMP	#8
	BNE	CHKHSL
	LDY	DL1LOC
	LDA	#$7D
	STA	(DLPTR),Y
	DEY
	DEY
	DEY
	LDA	#$FD
	STA	(DLPTR),Y
	STY	DL1LOC
	LDY	DL2LOC
	LDA	#$7D
	STA	(DLPTR),Y
	DEY
	DEY
	DEY
	LDA	#$FD
	STA	(DLPTR),Y
	STY	DL2LOC
	LDX	#0
	STX	VSLCNT
	LDY	#$40
X48	STX	VSCROL
CHKHSL	INC	HSLCNT
	LDA	HSLCNT
	CMP	#3
	BNE	CHGDL
	LDA	#0
	STA	HSLCNT
	DEC	SHSCRL
	LDA	SHSCRL
	BPL	X49
	INY
	LDA	#3
X49	STA	SHSCRL
	STA	HSCROL
;
CHGDL	STY	TEMP
	LDY	#4
LOOP21	LDA	(DLPTR),Y
	CLC
	ADC	TEMP
	STA	(DLPTR),Y
	INY
	BCC	X44
	LDA	(DLPTR),Y
	CLC
	ADC	#1
	STA	(DLPTR),Y
X44	INY
	INY
	BNE	X45
	INC	DLPTR+1
X45	CPY	#$24
	BNE	LOOP21
;
	LDA	FMCNTL
	CMP	#$CD
	BCS	X50
X51	JMP	ENDISR
X50	CMP	#$F1
	BNE	X27
	LDA	#$18
	STA	PRIOR
X27	INC	HNDCNT
	LDA	HNDCNT
	CMP	#5
	BNE	X51
	LDA	#0
	STA	HNDCNT
	LDA	HNDSTP
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	TAX
	LDA	MEMORG
	CLC
	ADC	#29
	STA	SCNPTR+1
	LDA	#43
	STA	SCNPTR
X54	LDY	#0
X53	LDA	HNDTAB,X
	STA	(SCNPTR),Y
	INX
	INY
	TXA
	AND	#$1F
	BEQ	X52
	CPY	#3
	BNE	X53
	LDA	SCNPTR
	CLC
	ADC	#$40
	STA	SCNPTR
	BCC	X54
	INC	SCNPTR+1
	BCS	X54
X52	INC	HNDSTP
	JMP	ENDISR
;
;
ROTSWD
	CLRPL	P0
	CLRPL	P1
	CLRPL	P2
	CLRPL	P3
	CLRPL	M
	RTSWD	P0,0
	RTSWD	P1,0
	RTSWD	P2,0
	RTSWD	P3,0
	LDX	STEP
	LDA	P3COLT,X
	STA	PCOLR3
	STA	COLPM3
	RTSWD	M,1,0
;
	JMP	NXTISR
;
REVERS
	REVRS	P0
	REVRS	P1
	REVRS	P2
	REVRS	P3
	REVRS	M
;
	NOP
	LDA	STEP
	CLC
	ADC	#$01
	AND	#$0F
	STA	STEP
	JMP	NXTISR
;
;termination to VBI service routine
;
ENDISR	LDA	BASEY
	CMP	#$40
	BCS	X12
	ADC	#$10
	BCS	X6
	ADC	DELTAY
	BCC	X11
X6	INC	BASEY
X11	STA	DELTAY
X12	LDA	#$FF
	SEC
	SBC	BASEX
	CMP	#$98
	BCC	X13
	SEC
	SBC	#$50
	CLC
	ADC	DELTAX
	BCC	X23
	INC	BASEX
X23	STA	DELTAX
X13	JMP	XITVBV
;
;stage two processing retracts sword
;
STAGE2	INC	FMCNT2
	LDA	FMCNT2
	CMP	#$58
	BCS	X34
X35	JMP	XITVBV
X34	CMP	#$FF
	BNE	X36
	DEC	FMCNT2
	BNE	X35
X36	AND	#3
	BNE	X35
	RETRC	P0
	RETRC	P3
	RETRC	M
	LDA	MEMORG
	CLC
	ADC	#31
	STA	SCNPTR+1
	STA	TEMP2
	LDA	#43
	STA	SCNPTR
	CLC
	ADC	#$40
	STA	TEMP1
	LDX	#$B
X33	LDY	#0
X29	LDA	(SCNPTR),Y
	STA	(TEMP1),Y
	INY
	CPY	#3
	BNE	X29
	LDA	SCNPTR+1
	STA	TEMP2
	LDA	SCNPTR
	STA	TEMP1
	SEC
	SBC	#$40
	STA	SCNPTR
	BCS	X32
	DEC	SCNPTR+1
X32	DEX
	BNE	X33
	JMP	XITVBV
;
;			MUSIC TABLES
F1:      
	DB	0
	DB	121	; C4
	DB	162	; G3
	DB	162	; G3
	DB	121	; C4
	DB	108	; D4
	DB	102	; Eb4
	DB	108	; D4
	DB	81	; G4
	DB	81	; G4
	DB	85	; Bb4
	DB	81	; G4
	DB	68	; Bb4
	DB	60	; C5
	DB	68	; Bb4
	DB	76	; Ab4
	DB	0
	DB	0
;

T1:
	DB	1
	DB	48*TEMPO
	DB	12*TEMPO
	DB	4*TEMPO
	DB	12*TEMPO
	DB	4*TEMPO
	DB	24*TEMPO
	DB	8*TEMPO
	DB	16*TEMPO
	DB	16*TEMPO
	DB	12*TEMPO
	DB	4*TEMPO
	DB	48*TEMPO
	DB	16*TEMPO
	DB	32*TEMPO
	DB	16*TEMPO
	DB	255
	DB	255
;
C1:
	DB	$01
	DB	$05
	DB	$07
	DB	$07
	DB	$07
	DB	$07
	DB	$07
	DB	$07
	DB	$09
	DB	$0B
	DB	$0B
	DB	$0B
	DB	$0D
	DB	$0D
	DB	$0D
	DB	$0D
	DB	$01
	DB	$01
;
F2:
	DB	0
	DB	162	; G3
	DB	217	; D3
	DB	217	; D3
	DB	162	; G3
	DB	162	; G3
	DB	121	; C4
	DB	162	; G3
	DB	102	; Eb4
	DB	102	; Eb4
	DB	108	; D4
	DB	102	; Eb4
	DB	96	; En4
	DB	81	; G4
	DB	96	; En4
	DB	96	; En4
	DB	96	; En4
	DB	91	; F4
	DB	0
	DB	0
;
T2:
	DB	1
	DB	48*TEMPO
	DB	12*TEMPO
	DB	4*TEMPO
	DB	12*TEMPO
	DB	4*TEMPO
	DB	24*TEMPO
	DB	8*TEMPO
	DB	16*TEMPO
	DB	16*TEMPO
	DB	12*TEMPO
	DB	4*TEMPO
	DB	24*TEMPO
	DB	16*TEMPO
	DB	8*TEMPO
	DB	16*TEMPO
	DB	32*TEMPO
	DB	16*TEMPO
	DB	255
	DB	255
;
C2:
	DB	$01
	DB	$05
	DB	$07
	DB	$07
	DB	$07
	DB	$07
	DB	$07
	DB	$07
	DB	$09
	DB	$0B
	DB	$0B
	DB	$0B
	DB	$0D
	DB	$0D
	DB	$0D
	DB	$0D
	DB	$0D
	DB	$0D
	DB	$01
	DB	$01
;
F3:
	DB	0
	DB	243	; C3   DRUM BEAT
	DB	243
	DB	0
	DB	243
	DB	243
	DB	0
	DB	243
	DB	243
	DB	0
	DB	243
	DB	243
	DB	0
	DB	243
	DB	243
	DB	0
	DB	243
	DB	243
	DB	0
	DB	243
	DB	243
	DB	0
	DB	243
	DB	243
	DB	0
	DB	243
	DB	243
	DB	0
	DB	243
	DB	243
	DB	0
	DB	0
	DB	0
;
T3:
	DB	1
	DB	4*TEMPO
	DB	4*TEMPO
	DB	20*TEMPO
	DB	4*TEMPO
	DB	8*TEMPO
	DB	24*TEMPO
	DB	4*TEMPO
	DB	4*TEMPO
	DB	8*TEMPO
	DB	4*TEMPO
	DB	8*TEMPO
	DB	20*TEMPO
	DB	4*TEMPO
	DB	4*TEMPO
	DB	8*TEMPO
	DB	4*TEMPO
	DB	4*TEMPO
	DB	24*TEMPO
	DB	4*TEMPO
	DB	8*TEMPO
	DB	4*TEMPO
	DB	4*TEMPO
	DB	4*TEMPO
	DB	40*TEMPO
	DB	4*TEMPO
	DB	4*TEMPO
	DB	20*TEMPO
	DB	4*TEMPO
	DB	8*TEMPO
	DB	8*TEMPO
	DB	255
	DB	255
;
P0XOFF	DB	20,12,13,12
	DB	12,12,13,12
	DB	20,29,28,29
	DB	29,29,28,29
P0YOFF	DB	5,27,24,23
	DB	21,16,13,13
	DB	12,13,13,16
	DB	21,23,24,27
P0TOFF	DB	0,$21,$2B,$38
	DB	$43,$38,$2B,$21
	DB	0,$4B,$55,$62
	DB	$6D,$62,$55,$4B
P1XOFF	DB	0,20,21,20
	DB	20,20,21,20
	DB	0,21,20,21
	DB	21,21,20,21
P1YOFF	DB	0,11,16,19
	DB	21,19,17,12
	DB	0,12,17,19
	DB	21,19,16,11
P1TOFF	DB	0,1,$1C,$2D
	DB	$39,$2D,$1C,1
	DB	0,$41,$5C,$6D
	DB	$79,$6D,$5C,$41
P2XOFF	DB	0,28,29,28
	DB	28,28,29,28
	DB	0,13,12,13
	DB	13,13,12,13
P2YOFF	DB	0,7,12,15
	DB	21,23,25,24
	DB	0,24,25,23
	DB	21,15,12,7
P2TOFF	DB	0,1,$14,$21
	DB	$2D,$21,$14,1
	DB	$0,$35,$48,$55
	DB	$61,$55,$48,$35
P3XOFF	DB	23,20,13,36
	DB	36,36,13,20
	DB	22,21,28,5
	DB	5,5,28,21
P3YOFF	DB	37,36,32,13
	DB	21,27,9,8
	DB	4,8,9,27
	DB	21,13,32,36
P3TOFF	DB	0,9,15,$18
	DB	$22,$18,15,9
	DB	0,$2D,$33,$3C
	DB	$46,$3C,$33,$2D
P3COLT	DB	$46,$46,$46,$8F
	DB	$8F,$8F,$46,$46
	DB	$46,$46,$46,$8F
	DB	$8F,$8F,$46,$46
MXOFF	DB	18,12,5,8
	DB	4,8,5,12
	DB	18,28,36,33
	DB	36,33,36,28
MYOFF	DB	37,33,29,24
	DB	18,13,9,5
	DB	10,5,9,13
	DB	18,24,29,33
MTOFF	DB	0,3,$F,$1B
	DB	$28,$1B,$F,3
	DB	0,$3F,$4B,$57
	DB	$64,$57,$4B,$3F
P0TAB	DB	8,$1C,$3E,$7F
	DB	127,127,127,127
	DB	127,127,127,127
	DB	127,127,127,127
	DB	127,127,127,127
	DB	127,127,127,127
	DB	127,127,127,127
	DB	127,127,127,127
	DB	0,1,1,3
	DB	3,7,7,15,15
	DB	3,0,1,3,7
	DB	15,31,63,127
	DB	255,127,$3E,$1C
	DB	8,0,3,15
	DB	63,127,255,127
	DB	127,63,$3C,16
	DB	0,255,255,255
	DB	255,255,255,255
	DB	0,192,240,240
	DB	224,224,192,192,128
	DB	128,0,16,56,124
	DB	254,255,254,252
	DB	248,240,224,192
	DB	128,0,8,60
	DB	252,254,254,255
	DB	254,252,240,192
	DB	0,255,255,255
	DB	255,255,255,255
	DB	0
P1TAB	DB	0,1,1,3
	DB	3,7,7,15
	DB	15,31,31,63
	DB	63,127,127,255
	DB	255,254,254,252
	DB	252,248,248,240
	DB	240,224,96
	DB	0,1,3,7
	DB	15,31,63,127
	DB	255,255,254,252
	DB	248,240,224,192
	DB	128,0,3,15
	DB	63,255,255,255
	DB	255
	DB	255,252,240,192
	DB	0,255,255,255
	DB	255,255,255,255
	DB	0,6,7,15
	DB	15,31,31,63
	DB	63,127,127,255
	DB	255,254,254,252
	DB	252,248,248,240
	DB	240,224,224,192
	DB	192,128,128,0
	DB	1,3,7,15
	DB	31,63,127,255
	DB	255,254,252,248
	DB	240,224,192,128
	DB	0,3,15,63
	DB	255,255,255,255
	DB	255,252,240,192
	DB	0,255,255,255
	DB	255,255,255,255
	DB	0
P2TAB	DB	0,12,$3C,$FC
	DB	254,254,254,252
	DB	252,248,248,240
	DB	240,224,224,192
	DB	192,128,128,0
	DB	31,63,127,255
	DB	255,254,252,248
	DB	240,224,192,128
	DB	0,3,15,63
	DB	255,255,255,255
	DB	255,252,240,192
	DB	0,255,255,255
	DB	255,255,255,255
	DB	0,1,1,3
	DB	3,7,7,15
	DB	15,31,31,63
	DB	63,127,127,127
	DB	63,60,48,0
	DB	1,3,7,15
	DB	31,63,127,255
	DB	255,254,252,248
	DB	0,3,15,63
	DB	255,255,255,255
	DB	255,252,240,192
	DB	0,255,255,255
	DB	255,255,255,255
	DB	0
P3TAB	DB	255,255,224,224,224
	DB	224,224,224,0,128
	DB	240,248,120,$18
	DB	0,128,192,224
	DB	240,248,$9C,$0E
	DB	6,0,48,254
	DB	254,254,252,252
	DB	252,240,192,0
	DB	248,252,254,255
	DB	254,252,248,0
	DB	31,31,0,24
	DB	30,31,15,1
	DB	0,96,112,57
	DB	31,15,7,3
	DB	1,0,3,15
	DB	63,63,63,127
	DB	127,127,12,0
	DB	31,63,127,255
	DB	127,63,31,0
MTAB	DB	248,248
	DB	0,192,224,252
	DB	127,31,15,31
	DB	30,$3E,$3C,12
	DB	0,12,14,7
	DB	3,1,1,3
	DB	7,15,15,14
	DB	0,192,224,112
	DB	48,56,24,$7C
	DB	252,$EE,$CE,7,3
	DB	0,7,7,7
	DB	7,7,255,255
	DB	255,7,7,7
	DB	7,7,0
	DB	255,255,224,224
	DB	224,224,224,224
	DB	0,48,60,124
	DB	120,248,240,248
	DB	254,31,7,3
	DB	0,112,240,240,224
	DB	192,128,128,192
	DB	224,112,48
	DB	0,192,224,115
	DB	119,63,62,24
	DB	28,12,14,7,3
	DB	0,224,224,224
	DB	224,224,255,255
	DB	255,224,224,224
	DB	224,224,0
;
BASET	DB	$10,$58,$88,$C8
TMPLTL	DB	19,17,15,13,11
	DB	10,9,8,7,6
	DB	7,8,10,0
	DB	72,67,58,57,45
	DB	46,39,36,31,29
	DB	28,25,22,21,22
	DB	23,21,16,12,10
	DB	9,10,12,12,11
	DB	8,6,5,4,3
	DB	3,2,1,0,0
	DB	0,0,0,0,0
	DB	0,0,0,0,0
	DB	0,0,0,0,0
TMPLTR	DB	31,33,35,37,39
	DB	40,41,42,43,44
	DB	43,42,40,0
	DB	98,103,112,113,125
	DB	124,131,134,139,141
	DB	142,145,148,149,148
	DB	147,149,154,158,160
	DB	161,160,158,158,159
	DB	162,164,165,166,167
	DB	167,168,169,170,170
	DB	170,170,170,170,170
	DB	170,170,170,170,170
	DB	170,170,170,0
HNDTAB	DB	0,0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,$11,$10
	DB	0,$11,$10
	DB	0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,$11,$10
	DB	0,$11,$10
	DB	0,$11,$10
	DB	0,$15,$51
	DB	0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,$11,$10
	DB	0,$11,$10
	DB	0,$11,$10
	DB	0,$15,$51
	DB	0,$15,$51
	DB	0,$15,$55
	DB	0,0
	DB	0,0,0
	DB	0,0,0
	DB	0,$11,$10
	DB	0,$11,$10
	DB	0,$11,$10
	DB	0,$15,$51
	DB	0,$15,$51
	DB	0,$15,$55
	DB	0,$15,$55
	DB	0,5,$50
	DB	0,0
	DB	0,0,0
	DB	0,0,$40
	DB	0,4,$40
	DB	0,$44,$10
	DB	0,$41,$11
	DB	0,$11,$11
	DB	0,$15,$55
	DB	0,$15,$55
	DB	0,$15,$50
	DB	0,5,$50
	DB	0,0
	DB	0,0,0
	DB	1,$41,0
	DB	$14,$51,0
	DB	5,$15,0
	DB	$11,$55,$40
	DB	4,$55,$40
	DB	1,$55,$50
	DB	0,$55,$50
	DB	0,5,$50
	DB	0,5,$50
	DB	0,0
	DB	0,$54,0
	DB	0,$14,0
	DB	$55,$55,0
	DB	1,$55,$40
	DB	$55,$55,$40
	DB	1,$55,$50
	DB	$55,$55,$50
	DB	0,$15,$50
	DB	0,5,$50
	DB	0,5,$50
	DB	0,0
	DB	0,0,0
	DB	0,$15,0
	DB	5,$45,$40
	DB	0,5,$40
	DB	5,$45,$50
	DB	0,5,$50
	DB	5,$45,$50
	DB	0,5,$50
	DB	0,5,$54
	DB	0,5,$54
	DB	0,0

	END
